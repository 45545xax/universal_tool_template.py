// Fully auto install, with Python script name inside this file name as "install_PythonFileName.mel"
global proc Tool_Locator (){} // a catcher empty functon for get cmd path
global proc string[] Tool_Location_v2 (){
    string $whatIs=`whatIs Tool_Locator`; // actual main dir path get function
    string $fullPath=`substring $whatIs 25 999`;
    string $buffer[];
    int $numTok=`tokenize $fullPath "/" $buffer`;
    int $numLetters=size($fullPath);
    int $numLettersLastFolder=size($buffer[ $numTok - 1 ]);
    string $scriptLocation=`substring $fullPath 1 ($numLetters-$numLettersLastFolder)`;
    string $script_name_mel = $buffer[ $numTok - 1 ];
    string $bufferB[];
    int $numTokB=`tokenize $script_name_mel "_." $bufferB`;
    string $script_name = $bufferB[ $numTokB - 2 ];
    return {$scriptLocation,$script_name};
}
// - step 1: prepare shelf
global string $gShelfTopLevel;
string $currentShelf = `tabLayout -query -selectTab $gShelfTopLevel`;
setParent $currentShelf;
// - step 2: prepare path, name, icon, cmd
string $tool_info[] = `Tool_Location_v2`;
string $icon=$tool_info[0] + "icons/" + $tool_info[1] +".png";
string $cmd ="import sys;myPath='"+$tool_info[0]+"';myPath in sys.path or sys.path.append(myPath);";
$cmd+="import "+$tool_info[1]+";"+$tool_info[1]+".main()";
// - step 3: create shelf btn
shelfButton -c $cmd -ann $tool_info[1] -l $tool_info[1] -image $icon -image1 $icon -sourceType "python";
print ("\n// "+$tool_info[1]+" has been added to current shelf.\n");
